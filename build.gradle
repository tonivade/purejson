plugins {
  id 'java-library'
  id 'jacoco'
  id 'maven-publish'
  id 'signing'
  alias(libs.plugins.nexus)
  alias(libs.plugins.versions)
  alias(libs.plugins.errorprone)
}

group = 'com.github.tonivade'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url = 'https://central.sonatype.com/repository/maven-snapshots/'
  }
}

jacoco {
  toolVersion = '0.8.11'
}

compileJava {
  options.compilerArgs << '-Xlint:unchecked'
  options.compilerArgs << '-Xlint:rawtypes'
  options.release = 21
}

compileTestJava {
  options.compilerArgs << '-Xlint:unchecked'
  options.compilerArgs << '-Xlint:rawtypes'
  options.release = 21
}

jacocoTestReport {
  reports {
    xml.required = true
    html.required = true
    xml.outputLocation = file("${buildDir}/reports/jacoco/report.xml")
  }
}

java {
  withJavadocJar()
  withSourcesJar()
}

test {
  useJUnitPlatform() {
    excludeTags "performance"
  }

  jvmArgs([
    '--add-opens=jdk.compiler/com.sun.tools.javac.api=ALL-UNNAMED',
    '--add-opens=jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED',
    '--add-opens=jdk.compiler/com.sun.tools.javac.main=ALL-UNNAMED'])

  jacoco {
    destinationFile = file("${buildDir}/jacoco/test.exec")
  }

  reports.html.required = true
}

jacocoTestReport {
  reports {
    xml.required = true
    html.required = true
    xml.outputLocation = file("${buildDir}/reports/jacoco/report.xml")
  }
}

tasks.withType(JavaCompile) {
  if (!name.toLowerCase().contains("test")) {
    options.errorprone {
      check("NullAway", net.ltgt.gradle.errorprone.CheckSeverity.ERROR)
      option("NullAway:AnnotatedPackages", "com.github.tonivade.purejson")
      option("NullAway:SuggestSuppressions", "true")
    }
  } else {
    options.errorprone.enabled = false
  }
}

dependencies {
  errorprone libs.nullaway
  errorprone libs.errorprone

  api libs.purefunCore
  api libs.minimalJson
  api libs.javapoet
  
  testImplementation platform(libs.junitBom)
  testImplementation libs.compileTesting
  testImplementation libs.junitJupiter
  testRuntimeOnly libs.junitLauncher
  testImplementation libs.purecheck
  testImplementation libs.purefunMonad
  testImplementation libs.purefunInstances
  testImplementation libs.gson
}

publishing {
  publications {
    mavenJava(MavenPublication) {

      from components.java

      pom {
        name = 'PureJson'
        description = 'Pure Functional Json Parser Library'
        url = 'https://github.com/tonivade/purejson'
        inceptionYear = '2020'

        scm {
          url = 'https://github.com/tonivade/purejson'
          connection = 'scm:https://github.com/tonivade/purejson.git'
          developerConnection = 'scm:git://github.com/tonivade/purejson.git'
        }

        licenses {
          license {
            name = 'The MIT License (MIT)'
            url = 'https://github.com/tonivade/purejson/blob/master/LICENSE'
            distribution = 'repo'
          }
        }

        developers {
          developer {
            id = 'tonivade'
            name = 'Antonio MuÃ±oz'
            email = 'antoniogmc@gmail.com'
          }
        }
      }
    }
  }
}

signing {
  def signingKey = project.findProperty("signingKey")
  def signingPassword = project.findProperty("signingPassword")
  useInMemoryPgpKeys(signingKey, signingPassword)
  sign publishing.publications.mavenJava
}

tasks.withType(Sign) {
  onlyIf { isReleaseVersion }
}

nexusPublishing {
  repositories {
    sonatype {
      nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
      snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
    }
  }

  clientTimeout = Duration.ofSeconds(300)
  connectTimeout = Duration.ofSeconds(60)

  transitionCheckOptions {
    maxRetries.set(40)
    delayBetween.set(Duration.ofMillis(3000))
  }
}
